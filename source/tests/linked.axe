use stdlib/arena (
    Arena,
    arena_create,
    arena_destroy,
    arena_alloc
);

use stdlib/memory (
    size_of
);

model Node {
    value: int;
    next: ref Node;
}

/// Allocate a new Node from the arena and initialize it
def new_node(arena: ref Arena, value: int): ref Node {
    mut val n: ref Node = arena_alloc(arena, size_of(Node));
    n.value = value;
    n.next = nil;
    return n;
}

/// Push a new value to the front of the list
def list_push_front(head: ref Node, arena: ref Arena, value: int): ref Node {
    mut val new_head: ref Node = new_node(arena, value);
    new_head.next = head;
    return new_head;
}

/// Push a new value to the back of the list
def list_push_back(head: ref Node, arena: ref Arena, value: int): ref Node {
    if head == nil {
        return new_node(arena, value);
    }

    mut val cur: ref Node = head;
    loop {
        if cur.next == nil {
            break;
        }
        cur = cur.next;
    }

    cur.next = new_node(arena, value);
    return head;
}

/// Print linked list values
def list_print(head: ref Node) {
    mut val cur: ref Node = head;

    loop {
        if cur == nil {
            break;
        }
        print cur.value;
        print " ";
        cur = cur.next;
    }

    println "";
}

main {
    mut val arena: Arena = arena_create(65536);
    mut val head: ref Node = nil;

    head = list_push_front(head, ref_of(arena), 10);
    head = list_push_front(head, ref_of(arena), 5);
    head = list_push_back(head, ref_of(arena), 20);
    head = list_push_back(head, ref_of(arena), 25);

    println "List contents:";
    list_print(head);

    arena_destroy(ref_of(arena));
    println "Arena destroyed.";
}
