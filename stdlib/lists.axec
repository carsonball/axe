use stdlib/arena (
    Arena
);

use stdlib/string (
    String
);

macro make_list(name: string, type: string, nullval: untyped) {
    model name {
        data: ref type;
        length: i32;
        cap: i32;

        def new_list(arena: ref Arena, capacity: i32): ref name {
            mut val lst: ref name;
            raw {
                lst = (name*)stdlib_arena_Arena_alloc(arena, sizeof(name));
                lst->data = (type*)stdlib_arena_Arena_alloc_array(arena, sizeof(type), capacity);
            }
            lst.length = 0;
            lst.cap = capacity;
            return lst;
        }

        def push(lst: ref name, arena: ref Arena, value: type) {
            if lst.length >= lst.cap {
                val new_cap: i32 = lst.cap * 2;
                mut val new_data: ref type;
                raw {
                    new_data = (type*)stdlib_arena_Arena_alloc_array(arena, sizeof(type), new_cap);
                    for (int i = 0; i < lst->length; i++) {
                        memcpy(&new_data[i], &lst->data[i], sizeof(type));
                    }
                }
                lst.data = new_data;
                lst.cap = new_cap;
            }
            lst.data[lst.length] = value;
            lst.length = lst.length + 1;
        }

        def get(lst: ref name, index: i32): type {
            if index < 0 or index >= lst.length {
                println "Index out of bounds!";
                return nullval;
            }
            return lst.data[index];
        }

        def print_all(lst: ref name) {
            for mut val i = 0; i < lst.length; i++ {
                print lst.data[i];
                print " ";
            }
            println "";
        }
    }
}

make_list(IntList, i32, 0);
make_list(FloatList, f32, 0.0);
make_list(CharList, char, "");
make_list(LongList, i64, 0);
make_list(DoubleList, f64, 0.0);

test {
    mut val arena: Arena = Arena.create(65536);
    mut val my_list: ref IntList = IntList.new_list(ref_of(arena), 4);

    IntList.push(my_list, ref_of(arena), 10);
    IntList.push(my_list, ref_of(arena), 20);
    IntList.push(my_list, ref_of(arena), 30);
    IntList.push(my_list, ref_of(arena), 40);
    IntList.push(my_list, ref_of(arena), 50);

    println "IntList contents:";
    IntList.print_all(my_list);

    println "Element at index 2:";
    println IntList.get(my_list, 2);

    Arena.destroy(ref_of(arena));
}
